import MIL.Common
import Mathlib.Data.Nat.Factorization.Basic
import Mathlib.Data.Nat.Prime.Basic

#print Nat.Coprime

example (m n : Nat) (h : m.Coprime n) : m.gcd n = 1 :=
  h

example (m n : Nat) (h : m.Coprime n) : m.gcd n = 1 := by
  rw [Nat.Coprime] at h
  exact h

example : Nat.Coprime 12 7 := by norm_num

example : Nat.gcd 12 8 = 4 := by norm_num

#check Nat.prime_def_lt

example (p : ℕ) (prime_p : Nat.Prime p) : 2 ≤ p ∧ ∀ m : ℕ, m < p → m ∣ p → m = 1 := by
  rwa [Nat.prime_def_lt] at prime_p

#check Nat.Prime.eq_one_or_self_of_dvd

example (p : ℕ) (prime_p : Nat.Prime p) : ∀ m : ℕ, m ∣ p → m = 1 ∨ m = p :=
  prime_p.eq_one_or_self_of_dvd

example : Nat.Prime 17 := by norm_num

-- commonly used
example : Nat.Prime 2 :=
  Nat.prime_two

example : Nat.Prime 3 :=
  Nat.prime_three

#check Nat.Prime.dvd_mul
#check Nat.Prime.dvd_mul Nat.prime_two
#check Nat.prime_two.dvd_mul

theorem even_of_even_sqr {m : ℕ} (h : 2 ∣ m ^ 2) : 2 ∣ m := by
  rw [pow_two, Nat.prime_two.dvd_mul] at h
  cases h <;> assumption

example {m : ℕ} (h : 2 ∣ m ^ 2) : 2 ∣ m :=
  Nat.Prime.dvd_of_dvd_pow Nat.prime_two h

example (a b c : Nat) (h : a * b = a * c) (h' : a ≠ 0) : b = c :=
  -- apply? suggests the following:
  (mul_right_inj' h').mp h

#check mul_right_inj
#check mul_right_inj'

-- See if you can fill out the proof sketch, using even_of_even_sqr and the
-- theorem Nat.dvd_gcd.
example {m n : ℕ} (coprime_mn : m.Coprime n) : m ^ 2 ≠ 2 * n ^ 2 := by
  intro sqr_eq
  have two_dvd_m : 2 ∣ m := by
    apply even_of_even_sqr
    have : 2 * n ^ 2 = m ^ 2 := by rw [sqr_eq]
    exact dvd_of_mul_right_eq (n ^ 2) this
  obtain ⟨k, meq⟩ := dvd_iff_exists_eq_mul_left.mp two_dvd_m
  have two_neq_zero: 2 ≠ 0 := by norm_num
  have : 2 * (2 * k ^ 2) = 2 * n ^ 2 := by
    rw [← sqr_eq, meq]
    ring
  have : 2 * k ^ 2 = n ^ 2 := by
    exact (Nat.mul_right_inj two_neq_zero).mp this
  have two_dvd_n : 2 ∣ n := by
    apply even_of_even_sqr
    exact dvd_of_mul_right_eq (k ^ 2) this
  have gcd_mn_one : m.gcd n = 1 := by exact coprime_mn
  have two_dvd_gcd: 2 ∣ m.gcd n := Nat.dvd_gcd two_dvd_m two_dvd_n
  have : 2 ∣ 1 := by
    rw [← gcd_mn_one]
    exact two_dvd_gcd
  norm_num at this

example {m n p : ℕ} (coprime_mn : m.Coprime n) (prime_p : p.Prime) : m ^ 2 ≠ p * n ^ 2 := by
  -- copy and paste above with a few modifications
  intro sqr_eq
  have p_dvd_m : p ∣ m := by
    apply Nat.Prime.dvd_of_dvd_pow
    apply prime_p
    rw [sqr_eq]
    exact Nat.dvd_mul_right p (n ^ 2)
  obtain ⟨k, meq⟩ := dvd_iff_exists_eq_mul_left.mp p_dvd_m
  have p_neq_zero: p ≠ 0 := by
    exact Nat.Prime.ne_zero prime_p
  have : p * (p * k ^ 2) = p * n ^ 2 := by
    rw [← sqr_eq, meq]
    ring
  have : p * k ^ 2 = n ^ 2 := by
    exact (Nat.mul_right_inj p_neq_zero).mp this
  have p_dvd_n : p ∣ n := by
    apply Nat.Prime.dvd_of_dvd_pow
    apply prime_p
    exact dvd_of_mul_right_eq (k ^ 2) this
  have gcd_mn_one : m.gcd n = 1 := by exact coprime_mn
  have p_dvd_gcd: p ∣ m.gcd n := Nat.dvd_gcd p_dvd_m p_dvd_n
  have : p ∣ 1 := by
    rw [← gcd_mn_one]
    exact p_dvd_gcd
  have p_one: p = 1 := by exact Nat.eq_one_of_dvd_one this
  have p_ge_one: p > 1 := by exact Nat.Prime.one_lt prime_p
  have p_ne_one : p ≠ 1 := by exact Ne.symm (Nat.ne_of_lt p_ge_one)
  contradiction

#check Nat.primeFactorsList
#check Nat.prime_of_mem_primeFactorsList
#check Nat.prod_primeFactorsList
#check Nat.primeFactorsList_unique

theorem factorization_mul' {m n : ℕ} (mnez : m ≠ 0) (nnez : n ≠ 0) (p : ℕ) :
    (m * n).factorization p = m.factorization p + n.factorization p := by
  rw [Nat.factorization_mul mnez nnez]
  rfl

theorem factorization_pow' (n k p : ℕ) :
    (n ^ k).factorization p = k * n.factorization p := by
  rw [Nat.factorization_pow]
  rfl

theorem Nat.Prime.factorization' {p : ℕ} (prime_p : p.Prime) :
    p.factorization p = 1 := by
  rw [prime_p.factorization]
  simp

example {m n p : ℕ} (nnz : n ≠ 0) (prime_p : p.Prime) : m ^ 2 ≠ p * n ^ 2 := by
  intro sqr_eq
  have nsqr_nez : n ^ 2 ≠ 0 := by simpa
  have eq1 : Nat.factorization (m ^ 2) p = 2 * m.factorization p := by
    simp
  have eq2 : (p * n ^ 2).factorization p = 2 * n.factorization p + 1 := by
    rw [factorization_mul']
    rw [factorization_pow']
    rw [Nat.Prime.factorization']
    rw [add_comm]
    apply prime_p
    exact Nat.Prime.ne_zero prime_p
    exact nsqr_nez
  have : 2 * m.factorization p % 2 = (2 * n.factorization p + 1) % 2 := by
    rw [← eq1, sqr_eq, eq2]
  rw [add_comm, Nat.add_mul_mod_self_left, Nat.mul_mod_right] at this
  norm_num at this

example {m n k r : ℕ} (nnz : n ≠ 0) (pow_eq : m ^ k = r * n ^ k) {p : ℕ} :
    k ∣ r.factorization p := by
  rcases r with _ | r
  · simp
  have npow_nz : n ^ k ≠ 0 := fun npowz ↦ nnz (pow_eq_zero npowz)
  have eq1 : (m ^ k).factorization p = k * m.factorization p := by
    exact factorization_pow' m k p
  have eq2 : ((r + 1) * n ^ k).factorization p =
      k * n.factorization p + (r + 1).factorization p := by
    rw [factorization_mul']
    rw [factorization_pow']
    rw [add_comm]
    exact r.succ_ne_zero
    exact npow_nz
  have : r.succ.factorization p = k * m.factorization p - k * n.factorization p := by
    rw [← eq1, pow_eq, eq2, add_comm, Nat.add_sub_cancel]
  rw [this]
  apply Nat.dvd_sub
  exact Nat.dvd_mul_right k (m.factorization p)
  exact Nat.dvd_mul_right k (n.factorization p)


#check multiplicity
